apply plugin: 'com.android.application'
apply from: '../config/properties-util.gradle'

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // rename the apk with the version name
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent + "/${variant.buildType.name}",
                    "andfast-${variant.versionName}-${variant.buildType.name}.apk".toLowerCase())
        }
    }

    signingConfigs {
        if (propertyHaveSigningConfigs) {
            release {
                storeFile file(propertyStoreFileStr)
                storePassword propertyStorePwdStr
                keyAlias propertyKeyAliasStr
                keyPassword propertyKeyPwdStr
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false //混淆
            zipAlignEnabled true
            shrinkResources false // 移除无用的resource文件
            buildConfigField "boolean", "LOG_DEBUG", "false"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (propertyHaveSigningConfigs)
                signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true //混淆
            zipAlignEnabled true
            shrinkResources true // 移除无用的resource文件
            buildConfigField "boolean", "LOG_DEBUG", "true"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (propertyHaveSigningConfigs)
                signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:design:$rootProject.ext.supportVersion"
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    compile project(path: ':pullrecyclerview')
    compile project(path: ':statusbar')
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'org.greenrobot:eventbus:3.0.0'
}
